# SPDX-License-Identifier: BSD-2-Clause

year="2023"
month="02"
pkgname=odin
pkgver="dev_${year}_${month}"
pkgrel=1
pkgdesc="The Data-Oriented Language for Sane Software Development."
arch=(x86_64)
url="https://odin-lang.org/"
license=(BSD)
depends=(llvm-libs)
makedepends=(clang llvm)
provides=(odin)

source=("https://github.com/odin-lang/Odin/archive/refs/tags/dev-${year}-${month}.zip")
b2sums=('9a4ee41ebfd38e11f10734b434f28f9a7b2a8a942eac9efa37ad5f8304695b08be01910ea752c9a293549c028dedfd8f9ff94f5c1e5aa147d63599640b52bc71')

build() {
    cd Odin-*
    make release
}

package() {
    cd Odin-*
    install --directory "${pkgdir}/usr/lib/odin"

    # install odin binary. cannot place odin executable inside bin directory directly.
    # using symlink instead. see: https://odin-lang.org/docs/install/#for-mac-and-nix-1
    #    "The compiler currently relies on the core and shared library collection being
    #     relative to the compiler executable. Installing the compiler in the usual sense
    #     (to /usr/local/bin or similar) is therefore not as straight forward as you need
    #     to make sure the mentioned libraries are available."
    install --directory "${pkgdir}/usr/bin"
    install odin "${pkgdir}/usr/lib/odin/odin"
    ln --symbolic "/usr/lib/odin/odin" "${pkgdir}/usr/bin/odin"

    # copy libraries: https://github.com/odin-lang/Odin#packages
    cp --recursive core "${pkgdir}/usr/lib/odin"
    cp --recursive vendor "${pkgdir}/usr/lib/odin"

    # install license: https://wiki.archlinux.org/title/PKGBUILD#license
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/odin/LICENSE"
}
